
Функция ПолучитьФайлыПакетов(КаталогИсходников)
	
	ФайлыПакетов = НайтиФайлы(КаталогИсходников, "*.ospx", Истина);
	Сообщить("Найдено пакетов: " + ФайлыПакетов.Количество());
	
	Возврат ФайлыПакетов;
	
КонецФункции

Процедура ОпубликоватьПакетыВЦелевомКаталоге(Знач ЦелевойКаталог, Знач ФайлыПакетов)
	
	Для Каждого ФайлПакета Из ФайлыПакетов Цикл
		
		ОбъектКаталогПакета = Новый Файл(ФайлПакета.Путь);
		ИмяПакета = ОбъектКаталогПакета.Имя;
		Сообщить("Обрабатываю пакет " + ИмяПакета);
		
		ЦелевойКаталогПакета = Новый Файл(ОбъединитьПути(ЦелевойКаталог, ИмяПакета));
		Если Не ЦелевойКаталогПакета.Существует() Тогда
			Сообщить("Создаю каталог: 
			|" + ЦелевойКаталогПакета.ПолноеИмя);
			СоздатьКаталог(ЦелевойКаталогПакета.ПолноеИмя);
		КонецЕсли;
		
		ЦелевойФайл = ОбъединитьПути(ЦелевойКаталогПакета.ПолноеИмя, ФайлПакета.Имя);
		Сообщить("Копирую пакет в целевой каталог: 
		|" + ЦелевойФайл);
		КопироватьФайл(ФайлПакета.ПолноеИмя, ЦелевойФайл);
		
		ФайлПакетаПоУмолчанию = Новый Файл(ОбъединитьПути(ЦелевойКаталогПакета.ПолноеИмя, ИмяПакета + ".ospx"));
		Если ФайлПакетаПоУмолчанию.Существует() Тогда
			Сообщить("Удаляю файл: 
			|" + ФайлПакетаПоУмолчанию.ПолноеИмя);
			УдалитьФайлы(ФайлПакетаПоУмолчанию.ПолноеИмя);
		КонецЕсли;
		
		Сообщить("Копирую файл в версию по умолчанию:
		|" + ЦелевойФайл);
		КопироватьФайл(ЦелевойФайл, ФайлПакетаПоУмолчанию.ПолноеИмя);
		
		Сообщить("Обработка завершена " + ИмяПакета);
		Сообщить("----------------------");
		
	КонецЦикла;
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////

КаталогИсходников = АргументыКоманднойСтроки[0];
ЦелевойКаталог = АргументыКоманднойСтроки[1];

Если Не (Новый Файл(ЦелевойКаталог).Существует()) Тогда
	СоздатьКаталог(ЦелевойКаталог);
КонецЕсли;

ФайлыПакетов = ПолучитьФайлыПакетов(КаталогИсходников);

Если ФайлыПакетов.Количество() = 0 Тогда
	Сообщить("Нет пакетов для публикации");
	ЗавершитьРаботу(1);
КонецЕсли;

ОпубликоватьПакетыВЦелевомКаталоге(ЦелевойКаталог, ФайлыПакетов);