
///////////////////////////////////////////////////////////////////////////////////////////////
//
// Модуль отправки сообщений 
// Доступные варианты
//	- Канал SLACK
//	- SMS
//
// (с) BIA Technologies, LLC	
//
///////////////////////////////////////////////////////////////////////////////////////////////

Перем АвторизацияSLACK;
Перем АвторизацияSMS;

///////////////////////////////////////////////////////////////////////////////////////////////

Процедура ОтправитьСообщениеSMS(Адресат, Сообщение) Экспорт

	ОтправитьСообщение("sms", Адресат, Сообщение);

КонецПроцедуры

Процедура ОтправитьСообщениеSLACK(Адресат, Сообщение, ТипСообщения) Экспорт

	ОтправитьСообщение("slack", Адресат, Сообщение,, ТипСообщения);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////

Процедура ОтправитьСообщение(Протокол, Адресат, Сообщение, ТемаСообщения = "", ТипСообщения = "") Экспорт

	Если Протокол = ДоступныеПротоколы().slack Тогда

		ОтправитьСообщениеВКаналSLACK(Адресат, Сообщение, ТипСообщения)

	ИначеЕсли Протокол = ДоступныеПротоколы().sms Тогда

		ОтправитьСообщениеОператоруSMS(Адресат, Сообщение);

	Иначе

		ВызватьИсключение "Неизвестный протокол отправки: " + Протокол;

	КонецЕсли;

КонецПроцедуры

Процедура ОтправитьСообщениеОператоруSMS(Адресат, Знач Сообщение) Экспорт

	Если АвторизацияSMS = Неопределено Тогда

		ВызватьИсключение "Необходимо выполнить инициализацию транспорта SMS";

	КонецЕсли;

	URL = АвторизацияSMS.URL;
	ИмяСервера = АвторизацияSMS.ИмяСервера;
	
	ТелоЗапроса = СтрШаблон(АвторизацияSMS.ШаблонТелаЗапроса, 
		АвторизацияSMS.Логин,
		АвторизацияSMS.Пароль,
		Адресат,
		Сообщение,
		АвторизацияSMS.Подпись); 
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	HTTPЗапрос.АдресРесурса = АвторизацияSMS.URL;
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);

	HTTP = Новый HTTPСоединение(ИмяСервера);
	Ответ = HTTP.ОтправитьДляОбработки(HTTPЗапрос);

КонецПроцедуры

Процедура ОтправитьСообщениеВКаналSLACK(Канал, ТекстСообщения, ТипСообщения) Экспорт

	Если АвторизацияSLACK = Неопределено Тогда

		ВызватьИсключение "Необходимо выполнить инициализацию транспорта Slack";

	КонецЕсли;
	
	ИмяСервера = "slack.com";
	
	Прокси = Новый ИнтернетПрокси(ИСТИНА);
	
	URL = "api/chat.postMessage?channel=" 
		+ Канал 
		+ "&text=" + СформироватьТекстСообщенияSLACK(ТипСообщения, ТекстСообщения) 
		+ "&as_user=" + АвторизацияSLACK.Логин + "&token=" + АвторизацияSLACK.Ключ;
	

	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = URL;
	
	HTTP = Новый HTTPСоединение(ИмяСервера,,,, Прокси);
	Ответ = HTTP.Получить(HTTPЗапрос);

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////
// Инициализация
///////////////////////////////////////////////////////////////////////////////////////////////

Процедура ИнициализацияSLACK(Логин, Ключ)Экспорт

	АвторизацияSLACK = Новый Структура("Логин, Ключ", Логин, Ключ)

КонецПроцедуры

Процедура ИнициализацияSMS(КодОператора, Логин, Пароль, Подпись)Экспорт

	Если КодОператора = ДоступныеОператорыSMS().smsbliss Тогда

		АвторизацияSMS = Новый Структура("ШаблонТелаЗапроса, Логин, Пароль, Подпись", ПолучитьШаблонТелаЗапросаSMSBliss(), Логин, Пароль, Подпись);
		АвторизацияSMS.Вставить("ИмяСервера", "json.gate.smsbliss.ru");
		АвторизацияSMS.Вставить("URL", "send");

	ИначеЕсли КодОператора = ДоступныеОператорыSMS().infobip Тогда

		АвторизацияSMS = Новый Структура("ШаблонТелаЗапроса, Логин, Пароль, Подпись", ПолучитьШаблонТелаЗапросаInfobip(), Логин, Пароль, Подпись);
		АвторизацияSMS.Вставить("ИмяСервера", "api.infobip.com");
		АвторизацияSMS.Вставить("URL", "api/v3/sendsms/json");

	Иначе

		ВызватьИсключение "Неизвестный код оператора: " + КодОператора;
		
	КонецЕсли;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////

Функция СформироватьТекстСообщенияSLACK(ТипСообщения, ТекстСообщения)

	Сообщение = ПолучитьИконкуТипаСообщенияSLACK(ТипСообщения) + " " + КодироватьСтроку(ТекстСообщения, СпособКодированияСтроки.КодировкаURL);
	Возврат Сообщение;

КонецФункции 

Функция ПолучитьИконкуТипаСообщенияSLACK(ТипСообщения)

	Иконка = ТипСообщения;
	Если ТипСообщения = "Ошибка" Тогда
		
		Иконка = ":no_entry:";
		
	ИначеЕсли ТипСообщения = "Информация" Тогда
		
		Иконка = ":speech_balloon:";                                            
		
	ИначеЕсли ТипСообщения = "Предупреждение" Тогда
	
		Иконка = ":warning:";  
		
	КонецЕсли;
	
	Возврат Иконка;

КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

Функция ПолучитьШаблонТелаЗапросаSMSBliss()

	Возврат
		"{ 
		|""login"": ""%1"",
		|""password"": ""%2"", 
		|""messages"" :[
		|	{
		|	""clientId"": 0,
		|	""phone"": ""%3"",
		|	""text"": ""%4"",
		|	""sender"": ""%5""
		|	}]
		|}";

КонецФункции

Функция ПолучитьШаблонТелаЗапросаInfobip()

	Возврат
	"{ 
	|""authentication"": 
	|	{
	|	""username"": ""%1"", 
	|	""password"": ""%2""
	|	}, 
	|""messages"" :[
	|	{
	|	""sender"": ""%5"",
	|	""text"": ""%4"",
	|	""type"": ""longSMS"",
	|	""datacoding"": ""8"",
	|	""recipients"": [{
	|		""gsm"": ""%3""}]
	|	}
	|]
	|}";		

КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

Функция ДоступныеОператорыSMS()Экспорт

	Возврат Новый Структура("smsbliss, infobip", "smsbliss", "infobip")
	
КонецФункции

Функция ДоступныеПротоколы()Экспорт

	Возврат Новый Структура("slack, sms", "slack", "sms")
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

АвторизацияSLACK = Неопределено;
АвторизацияSMS = Неопределено;
