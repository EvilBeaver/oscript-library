/////////////////////////////////////////////////////////////////////////////
//
// Чтение конфигурационных файлов стартера 1С
//
/////////////////////////////////////////////////////////////////////////////

Перем мСодержимое;

/////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
//

Процедура Открыть(Знач Путь) Экспорт

	ЧтениеТекста = Новый ЧтениеТекста(Путь);
	
	мСодержимое = Новый Соответствие;
	
	СтрокаФайла = "";
	Пока Не СтрокаФайла = Неопределено Цикл
		
		СтрокаФайла = ЧтениеТекста.ПрочитатьСтроку();
		Если СтрокаФайла = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Данные = КлючИЗначение(СтрокаФайла);
		Если Данные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьПараметр(Данные);
		
	КонецЦикла;

КонецПроцедуры

Функция ПолучитьСписок(Знач Ключ) Экспорт
	
	Возврат мСодержимое[ВРЕГ(Ключ)];
	
КонецФункции

Функция ПолучитьЗначение(Знач Ключ) Экспорт
	
	Массив = мСодержимое[ВРЕГ(Ключ)];
	Если ТипЗнч(Массив) = Тип("Массив") и Массив.Количество() > 0 Тогда
		Возврат Массив[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры

Функция КлючИЗначение(Знач СтрокаТекста)

	Поз = Найти(СтрокаТекста, "=");
	Если Поз = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Новый Структура("Ключ,Значение", ВРЕГ(Лев(СтрокаТекста, Поз-1)), Сред(СтрокаТекста, Поз+1));

КонецФункции

Процедура ДобавитьПараметр(Знач КлючИЗначение)
	
	ТекЗначение = мСодержимое[КлючИЗначение.Ключ];
	Если ТекЗначение = Неопределено Тогда
		ТекЗначение = Новый Массив;
		мСодержимое[КлючИЗначение.Ключ] = ТекЗначение;
	КонецЕсли;
	
	ТекЗначение.Добавить(КлючИЗначение.Значение);
	
КонецПроцедуры
